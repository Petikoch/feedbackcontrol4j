plugins {
    id "com.jfrog.bintray" version '1.6'          // https://github.com/bintray/gradle-bintray-plugin
    id 'nu.studer.credentials' version '1.0.3'      // https://github.com/etiennestuder/gradle-credentials-plugin
    id "com.github.ben-manes.versions" version "0.13.0"
    id "com.github.hierynomus.license" version "0.11.0"
    id 'com.github.kt3k.coveralls' version '2.6.3'  // https://github.com/kt3k/coveralls-gradle-plugin
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

ext.artifactGroupId = 'ch.petikoch.libs'
ext.description = 'A small java 7+ standalone library to implement feedback control'

version = '2.0.1-SNAPSHOT'

ext.fileEncoding = 'UTF-8'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

def groovyVersion = '2.4.7'
def spockVersion = '1.0-groovy-2.4'
def cglibVersion = '3.2.4'

dependencies {
    testCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    testCompile("org.spockframework:spock-core:${spockVersion}") {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile "cglib:cglib:${cglibVersion}" // spock mocks
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

sourceSets.main.java.srcDirs = ['src/main/java']
sourceSets.test.java.srcDirs = []
sourceSets.main.groovy.srcDirs = []
sourceSets.test.groovy.srcDirs = ['src/test/groovy']

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

tasks.withType(GroovyCompile) {
    groovyOptions.encoding = project.fileEncoding
    options.encoding = project.fileEncoding
}

tasks.withType(JavaCompile) {
    options.encoding = project.fileEncoding
}

test {
    systemProperty 'file.encoding', project.fileEncoding
}

// http://forums.gradle.org/gradle/topics/set_maxparallelforks_to_number_of_cores_on_the_current_machine
tasks.withType(Test) {
    maxParallelForks = Runtime.getRuntime().availableProcessors()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

license {
    header = rootProject.file('config/HEADER_apache2.txt')
    strictCheck = true
}

def pomConfig = {
    name project.name
    description project.description
    url 'https://github.com/Petikoch/feedbackcontrol4j'
    inceptionYear '2015'

    scm { url 'https://github.com/Petikoch/feedbackcontrol4j.git' }

    developers {
        developer {
            id 'petikoch'
            name 'Peti Koch'
            email 'petikoch@gmail.com'
            url 'https://www.google.com/+PetiKoch'
        }
    }

/*    contributors {
        contributor {
            name 'John Doe'
        }
    }*/
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            groupId project.artifactGroupId
            artifactId project.name
            version project.version
            from components.java

            pom.withXml {

                asNode().children().last() + pomConfig

                def licencesNode = asNode().appendNode('licenses')
                def licenseNode = licencesNode.appendNode('license')
                licenseNode.appendNode('name', 'The Apache Software License, Version 2.0')
                licenseNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                licenseNode.appendNode('distribution', 'repo')

                // hack... build up the test dependencies ourselve
                // why? because maven-publish plugin creates crapy dependencies in pom.xml (gradle 2.2)
                // see http://stackoverflow.com/questions/20131915/publishing-artifact-from-gradle-project-to-bintray-maven-repository
                // solution: http://stackoverflow.com/questions/24743562/gradle-not-including-dependencies-in-published-pom-xml
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.testCompile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                    dependencyNode.appendNode('scope', 'test')
                }
            }

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenCustomPublication {
        destination = file("$buildDir/generated_pom.xml")
    }
}

bintray {
    user = credentials.bintrayUserName       // https://github.com/etiennestuder/gradle-credentials-plugin
    key = credentials.bintrayApiKey          // https://github.com/etiennestuder/gradle-credentials-plugin
    publications = ['mavenCustom']   // see publications closure

    publish true

    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description // doesn't work
        websiteUrl = 'https://github.com/Petikoch/feedbackcontrol4j'
        issueTrackerUrl = 'https://github.com/Petikoch/feedbackcontrol4j/issues'
        vcsUrl = 'https://github.com/Petikoch/feedbackcontrol4j.git'
        licenses = ['Apache-2.0']
        labels = ['java']
        publicDownloadNumbers = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}