plugins {
    id 'nu.studer.credentials' version '2.1'      // https://github.com/etiennestuder/gradle-credentials-plugin
    id 'com.github.hierynomus.license' version '0.15.0'  // https://github.com/hierynomus/license-gradle-plugin
    id "com.github.ben-manes.versions" version "0.38.0"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = "ch.petikoch.libs"
archivesBaseName = project.name
version = '3.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext["fileEncoding"] = 'UTF-8'
ext["signing.keyId"] = credentials.gpgKeyId
ext["signing.password"] = credentials.gpgKeyPassword
ext["signing.secretKeyRingFile"] = credentials.gpgKeyFile
ext["ossrhUsername"] = credentials.ossrhUsername
ext["ossrhPassword"] = credentials.ossrhPassword

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.7.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.assertj:assertj-core:3.19.0')
    testImplementation('org.awaitility:awaitility:4.0.3')
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

task javadocJar(type: Jar, dependsOn: classes) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

tasks.withType(JavaCompile) {
    options.encoding = project.fileEncoding
}

test {
    systemProperty 'file.encoding', project.fileEncoding
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// http://forums.gradle.org/gradle/topics/set_maxparallelforks_to_number_of_cores_on_the_current_machine
tasks.withType(Test) {
    maxParallelForks = Runtime.getRuntime().availableProcessors()
}

license {
    header = rootProject.file('config/HEADER_apache2.txt')
    strictCheck = true
}

// https://central.sonatype.org/pages/gradle.html
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name project.archivesBaseName
                packaging 'jar'
                description 'A small java 8+ standalone library to implement feedback control'
                url 'https://github.com/Petikoch/feedbackcontrol4j'

                scm {
                    connection = 'https://github.com/Petikoch/feedbackcontrol4j.git'
                    developerConnection = 'https://github.com/Petikoch/feedbackcontrol4j.git'
                    url = 'https://github.com/Petikoch/feedbackcontrol4j'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'petikoch'
                        name 'Peti Koch'
                        email 'petikoch@gmail.com'
                    }
                }
            }
        }
    }
}